# LinkedList Example

load "List";

dynamic LinkedList :: List +> {
	var head;

	# Adds element to list
	add(&e) => v from {
		head == null then {
			head = new Link(e);
		} else {
			curr := head;
			curr.next != null do { curr = curr.next };
			curr.next = new Link(e);
		};
		v &= e;
	};

	# Retrieves mutable element from list
	index(pos) => e from {
		e = null;
		curr := head;
		[1 :> pos] ->> {curr = curr.next};
		e &= curr.value;
	};

	# Removes element from list
	remove(pos) => {
		curr := head;
		prev := null;
		[0 : pos] ->> {
			prev = curr;
			curr = curr.next;
		};
		prev == null then head = curr.next;
		prev.next = curr.next;
	};

	# Retrieves length of the list
	size() => s from {
		s = 0;
		curr := head;
		curr != null do {
			curr = curr.next;
			s += 1;
		};
	};

	dynamic Link :: {
		value := null;
		next := null;

		init(&value) => {
			self.value &= value;
			next = null;
		};
	};
};